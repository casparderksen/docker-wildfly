# Embed offline server and start batch
embed-server
batch

# TOO secrets.jceks and keystore.jks as property

# Configure paths
/path=secrets.dir:add(path=security, relative-to=jboss.server.config.dir)
/path=keystore.dir:add(path=security, relative-to=jboss.server.config.dir)

# Create credential stores with dummy passwords (real passwords are stored at startup)
/subsystem=elytron/credential-store=WildflyCS:add(location="secrets.jceks", relative-to=secrets.dir, credential-reference={clear-text="${env.WILDFLY_MASTER_PASSWORD:changeit}"}, create=true)
/subsystem=elytron/credential-store=WildflyCS:add-alias(alias=keystore-password, secret-value="changeit")
/subsystem=elytron/credential-store=WildflyCS:add-alias(alias=key-password, secret-value="changeit")

# Configure keystore, key manager and SSL context (change TLS certificate at startup)
/subsystem=elytron/key-store=httpsKS:add(type=JKS, path=keystore.jks, relative-to=keystore.dir, credential-reference={store=WildflyCS, alias=keystore-password})
/subsystem=elytron/key-manager=httpsKM:add(key-store=httpsKS, algorithm="SunX509", alias-filter=localhost, credential-reference={store=WildflyCS, alias=key-password})
/subsystem=elytron/server-ssl-context=httpsSSC:add(key-manager=httpsKM, protocols=["TLSv1.2"])

# Enable HTTPS
/subsystem=undertow/server=default-server/https-listener=https:add(socket-binding=https, ssl-context=httpsSSC)

# Enable HTTPS and authentication for management interface
/core-service=management/management-interface=http-interface:write-attribute(name=secure-socket-binding, value=management-https)
/core-service=management/management-interface=http-interface:write-attribute(name=ssl-context, value=httpsSSC)

# Enable Elytron security domain
/subsystem=undertow/application-security-domain=other:add(security-domain=ApplicationDomain)

# Disable repeated deployment scanning
/subsystem=deployment-scanner/scanner=default:write-attribute(name=scan-interval, value=-1)

# Enable property replacement in JEE config files
/subsystem=ee:write-attribute(name=jboss-descriptor-property-replacement, value=true)
/subsystem=ee:write-attribute(name=spec-descriptor-property-replacement, value=true)

# Run batch and stop embedded server
run-batch
stop-embedded-server